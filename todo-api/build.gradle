import org.jooq.codegen.*

buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
        datasourceUrl = 'jdbc:mysql://localhost:3306'
        datasourceUser = 'root'
        datasourcePassword = 'root'
        schema = 'todos'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath"org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath"org.jooq:jooq-codegen:3.11.9"
        classpath "org.jooq:jooq-meta-extensions:3.11.9"
        classpath group: 'mysql', name: 'mysql-connector-java', version: '8.0.11'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin : 'org.flywaydb.flyway'

group = 'com.kazys'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    compile 'org.jooq:jooq:3.11.9'
    compile "org.springframework.boot:spring-boot-starter-actuator"
    runtime 'mysql:mysql-connector-java:8.0.11'
    implementation 'org.projectlombok:lombok:1.18.4'
    compile group: 'org.modelmapper', name: 'modelmapper', version: '0.7.5'
    compile group: 'org.modelmapper.extensions', name: 'modelmapper-jooq', version: '2.3.2'
    testCompile 'junit:junit:4.11'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
    url = "${datasourceUrl}?autoReconnect=true&useSSL=false"
    user = "${datasourceUser}"
    password = "${datasourcePassword}"
    schemas = ["${schema}"]
}

def writer = new StringWriter()
def xml = new groovy.xml.MarkupBuilder(writer)
        .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.11.0.xsd') {
    jdbc() {
        driver('com.mysql.cj.jdbc.Driver')
        url("${datasourceUrl}/${schema}?autoReconnect=true&useSSL=false")
        user("${datasourceUser}")
        password("${datasourcePassword}")
    }
    generator() {
        database() {
            inputSchema("${schema}")
            includes('.*')
        }
        generate([:]) {
            pojos true
            daos true
        }
        target() {
            packageName('com.kazys.todoapi')
            directory('src/main/java/db')
        }
    }
}

GenerationTool.generate(writer.toString())